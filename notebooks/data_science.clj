(ns data-science
  (:require [clojure.set :refer [join rename-keys project]]
            [clojure.string :as str]
            [dk.ative.docjure.spreadsheet :as ss]
            [kixi.stats.core :as kixi-stats]
            [kixi.stats.protocols :as kixi-p]
            [meta-csv.core :as csv]
            [next.jdbc :as jdbc]
            [next.jdbc.result-set :as rs]
            [nextjournal.clerk :as clerk]))

;; # ä¸€ä¸ªå°å‹æ•°æ®ç§‘å­¦ç¤ºä¾‹ ğŸ”¢
^{:nextjournal.clerk/visibility {:code :hide}}

;; # åœ¨æ•°æ®ä¸­æ¢ç´¢ä¸–ç•Œ

;; ç°å®æ•°æ®ç§‘å­¦ä¸­çš„ä¸€ä¸ªæŒ‘æˆ˜æ˜¯ä»ä¸åŒçš„æ¥æºä»¥è®¸å¤šä¸åŒçš„æ ¼å¼è·å–æ•°æ®ã€‚
;; åœ¨æœ¬notebookä¸­ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨ä»TSVæ–‡ä»¶ã€Excelç”µå­è¡¨æ ¼å’Œæ•°æ®åº“æŸ¥è¯¢ä¸­è·å–çš„æ•°æ®ï¼Œ
;; æ¢ç´¢ä¸€äº›å…³äºä¸–ç•Œçš„äº‹å®ã€‚

;; ## é¢„æœŸå¯¿å‘½

;; é¦–å…ˆï¼Œæˆ‘ä»¬å°†ä½¿ç”¨ [meta-csv](https://github.com/ngrunwald/meta-csv) åº“
;; è¯»å–åŒ…å«æœ€æ–° CIA World Factbook æ•°æ®çš„ TSV æ–‡ä»¶ã€‚
(def cia-factbook
  (csv/read-csv "./datasets/cia-factbook.tsv"))

;; åœ¨æ•°æ®æŸ¥çœ‹å™¨ä¸­å±•å¼€ç»“æœå‘Šè¯‰æˆ‘ä»¬ï¼Œæ„Ÿå…´è¶£çš„åˆ—ä¸­å­˜åœ¨ä¸€äº› `nil` å€¼ï¼Œ
;; å¹¶ä¸”æˆ‘ä»¬çš„ TSV å¯¼å…¥å™¨å› æ­¤è¢«æŠ›å‡ºå¼‚å¸¸ï¼Œå› æ­¤æ²¡æœ‰å°†æ•°å€¼åˆ—è½¬æ¢ä¸ºæ•°å­—ç±»å‹ã€‚

;; æˆ‘ä»¬å°†ä½¿ç”¨æ™®é€šçš„ Clojure åºåˆ—å‡½æ•°å¯¹è¿™ä¸ªè¡¨è¿›è¡Œä¸€äº›åå¤„ç†ï¼Œ
;; ä»¥è¿‡æ»¤æ‰åœ¨æˆ‘ä»¬çš„æ„Ÿå…´è¶£çš„åˆ—ä¸­å…·æœ‰ `nil` å€¼çš„è¡Œï¼Œé€‰æ‹©è¿™äº›è¡Œï¼Œ
;; å°†å­—ç¬¦ä¸²è½¬æ¢ä¸ºæ•°å­—ï¼Œå¹¶ä¸”â€”â€”å› ä¸ºæˆ‘ä»¬æ˜¯ Clojuristâ€”â€”å°†é”®è½¬æ¢ä¸ºå…³é”®å­—ã€‚
(def life-expectancy
  (->> cia-factbook
       (remove #(some nil? (map (partial get %) ["Country" "GDP/cap" "Life expectancy"])))
       (map #(sorted-map :country (str/trim (get % "Country"))
                         :gdp (read-string (get % "GDP/cap"))
                         :life-expectancy (read-string (get % "Life expectancy"))))))

;; åœ¨æ•°æ®ç»“æ„æµè§ˆå™¨ä¸­çœ‹èµ·æ¥ç›¸å½“ä¸é”™ï¼Œä½†æ˜¯ä»¥è¡¨æ ¼å½¢å¼è·å¾—æ¦‚è§ˆä¼šæ›´å®¹æ˜“ã€‚
;; å¹¸è¿çš„æ˜¯ï¼ŒClerk çš„å†…ç½®è¡¨æ ¼æŸ¥çœ‹å™¨èƒ½å¤Ÿæ¨æ–­å‡ºå¦‚ä½•è‡ªåŠ¨å¤„ç†æ‰€æœ‰æœ€å¸¸è§çš„è¡Œå’Œåˆ—é…ç½®ã€‚
(clerk/table life-expectancy)

;; æˆ‘ä»¬è¿˜å¯ä»¥ç»˜åˆ¶æ•°æ®å›¾ï¼Œä»¥æŸ¥çœ‹æˆ‘ä»¬çš„ä¸¤ä¸ªæ„Ÿå…´è¶£çš„å˜é‡ï¼ˆäººå‡ GDP å’Œé¢„æœŸå¯¿å‘½ï¼‰ä¹‹é—´æ˜¯å¦å­˜åœ¨ä»»ä½•å¯è§çš„ç›¸å…³æ€§ã€‚
(clerk/vl
 {:data {:values life-expectancy}
  :width 700
  :height 500
  :mark {:type :point}
  :encoding {:x {:field :gdp
                 :type :quantitative}
             :y {:field :life-expectancy
                 :type :quantitative}
             :tooltip {:field :country}}})

;; æ¯«ä¸å¥‡æ€ªï¼Œç”Ÿæ´»åœ¨ä¸€ä¸ªæå…¶è´«ç©·çš„å›½å®¶å¯¹é¢„æœŸå¯¿å‘½æœ‰è´Ÿé¢å½±å“ã€‚
;; å¦ä¸€æ–¹é¢ï¼Œçœ‹èµ·æ¥ä¸€æ—¦äººå‡ GDP è¶…è¿‡æ¯å¹´ 10-15k ç¾å…ƒï¼Œæƒ…å†µå°±å¼€å§‹è¶‹äºå¹³ç¼“ã€‚
;; è¿˜å‡ºç°äº†ä¸€äº›å…¶ä»–æœ‰è¶£çš„æ¨¡å¼ï¼šæ–°åŠ å¡å’Œæ—¥æœ¬çš„é¢„æœŸå¯¿å‘½ç›¸ä¼¼ï¼Œ
;; å°½ç®¡å‰è€…çš„ GDP æ˜¯åè€…çš„ä¸¤å€ï¼Œè€Œå¡å¡”å°”â€”â€”æ•°æ®é›†ä¸­äººå‡ GDP æœ€å¯Œè£•çš„å›½å®¶â€”â€”
;; ä¸å¤šç±³å°¼åŠ å…±å’Œå›½çš„å¹³å‡é¢„æœŸå¯¿å‘½ç›¸ä¼¼ã€‚

;; ## ä¸å¹³ç­‰

;; ç°åœ¨ï¼Œè®©æˆ‘ä»¬å°è¯•ä½¿ç”¨åŒ…å«æ¯ä¸ªå›½å®¶/åœ°åŒºçš„ GINI ç³»æ•°ï¼ˆä¸€ç§å¹¿æ³›ä½¿ç”¨çš„æ”¶å…¥ä¸å¹³ç­‰æŒ‡æ ‡ï¼‰çš„ç”µå­è¡¨æ ¼ä¸­çš„ä¿¡æ¯è¿›è¡Œç›¸åŒçš„å®éªŒã€‚
;; æˆ‘ä»¬å°†ä½¿ç”¨ä¸€ä¸ªåä¸º [Docjure](https://github.com/mjul/docjure) çš„åº“ï¼Œè¯¥åº“æä¾›å¯¹ Microsoft Office æ–‡ä»¶æ ¼å¼çš„è®¿é—®ã€‚

;; Docjure çš„ API æœ‰ç‚¹åº•å±‚ï¼Œå¹¶ä¸”æ²¡æœ‰ä½¿æ˜æ˜¾çš„ä»»åŠ¡å˜å¾—å®¹æ˜“ï¼Œ
;; å› æ­¤æˆ‘ä»¬å°†ä½¿ç”¨è¿™ä¸ªè¾…åŠ©å‡½æ•°ä½¿ä¸‹é¢çš„ä»£ç æ›´æ¸…æ™°ã€‚
;; æŸ¥çœ‹é€è¡Œæ³¨é‡Šä»¥äº†è§£æ­¤å‡½æ•°çš„å·¥ä½œåŸç†ã€‚
(defn load-first-sheet
  "å°† Excel ç”µå­è¡¨æ ¼çš„ç¬¬ä¸€ä¸ªå·¥ä½œè¡¨ä½œä¸º map çš„ seq è¿”å›ã€‚"
  [filename]
  (let [rows (->> (ss/load-workbook filename) ; åŠ è½½æ–‡ä»¶
                  (ss/sheet-seq)              ; æ–‡ä»¶ä¸­å·¥ä½œè¡¨çš„ seq
                  first                       ; å–ç¬¬ä¸€ä¸ªï¼ˆä¹Ÿæ˜¯å”¯ä¸€ä¸€ä¸ªï¼‰
                  ss/row-seq                  ; ä»ä¸­è·å–è¡Œ
                  (mapv ss/cell-seq))         ; æ¯è¡Œ -> å•å…ƒæ ¼çš„ seq
        ;; åˆ†ç¦»æ ‡é¢˜ä»¥ç”Ÿæˆ map çš„ seq
        headers   (mapv (comp keyword ss/read-cell) (first rows))]
    ;; æ˜ å°„è¡Œï¼Œåˆ›å»ºä»¥æ ‡é¢˜ä½œä¸ºé”®çš„æ–° map
    (mapv #(zipmap headers (map ss/read-cell %)) (rest rows))))

;; ç°åœ¨ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨å‡ è¡Œä»£ç æ¥ï¼š
;; 1. åŠ è½½ç”µå­è¡¨æ ¼æ•°æ®ã€‚
;; 2. ä½¿ç”¨ `clojure.set` çš„ `join` å‡½æ•°å°†æˆ‘ä»¬æ–°åŠ è½½çš„ GINI ç”µå­è¡¨æ ¼ä¸æˆ‘ä»¬å…ˆå‰å‡†å¤‡çš„é¢„æœŸå¯¿å‘½æ•°æ®ç»“åˆèµ·æ¥ï¼Œ
;;    å› ä¸ºå®ƒä»¬éƒ½æ˜¯å…·æœ‰ `:country` é”®çš„ map çš„åºåˆ—ã€‚
;; 3. åœ¨æ¯ä¸ª map ä¸­å…³è”ä¸€ä¸ª `:gini` é”®åˆ°ä¸–ç•Œé“¶è¡Œçš„æ•°å­—ï¼Œä½†å›é€€åˆ° CIA çš„ä¼°è®¡å€¼ã€‚
;;    ï¼ˆè¿™äº›ç±»å‹çš„å°å‹ç¼–ç¨‹ä»»åŠ¡æ˜¯æ•°æ®æ•´ç†çš„ä¸€ä¸ªæŒç»­ç‰¹å¾ã€‚ï¼‰
(def expectancy-and-gini
  (->> (load-first-sheet "datasets/countries-gini.xlsx")
       (join life-expectancy)
       (keep #(if-let [gini (or (:giniWB %) (:giniCIA %))]
                (assoc % :gini gini)
                nil))))

;; å±•å¼€ Clojure æ•°æ®ç»“æ„ä½¿å®ƒçœ‹èµ·æ¥å¯ä»¥ç”¨äºæˆ‘ä»¬çš„æ¯”è¾ƒã€‚
;; è®©æˆ‘ä»¬ç»˜åˆ¶æ•°æ®å›¾ï¼Œä»¥æŸ¥çœ‹ä»æœ€å¹³ç­‰åˆ°æœ€ä¸å¹³ç­‰çš„å›½å®¶/åœ°åŒºçš„åˆ—è¡¨ï¼š
(clerk/vl
 {:data {:values expectancy-and-gini}
  :width 600
  :height 1600
  :mark {:type :point}
  :encoding {:x {:field :gini
                 :type :quantitative}
             :y {:field :country
                 :type :nominal
                 :sort :x}
             :tooltip {:field :country}}})

;; ç°åœ¨æ¥çœ‹çœ‹ä¸å¹³ç­‰å’Œé¢„æœŸå¯¿å‘½æ˜¯å¦ç›¸å…³ï¼š
(clerk/vl
 {:data {:values expectancy-and-gini}
  :mark :rect
  :width 700
  :height 500
  :encoding {:x {:bin {:maxbins 25}
                 :field :life-expectancy
                 :type :quantitative}
             :y {:bin {:maxbins 25}
                 :field :gini
                 :type :quantitative}
             :color {:aggregate :count :type :quantitative}}
  :config {:view {:stroke :transparent}}})

;; çœ‹èµ·æ¥é•¿å¯¿å›½å®¶çš„å¤§éƒ¨åˆ†ä¹Ÿå¤„äºä¸å¹³ç­‰åˆ†å¸ƒçš„è¾ƒä½ä¸‰åˆ†ä¹‹äºŒã€‚
;; ç¨å¾®è¿‡æ»¤ä¸€ä¸‹æ˜¾ç¤ºï¼Œå”¯ä¸€çœŸæ­£é•¿å¯¿çš„å›½å®¶/åœ°åŒºçš„ä¸å¹³ç­‰ç³»æ•°é«˜äº ~50 çš„æ˜¯é¦™æ¸¯ã€‚
(clerk/table
 (->> (filter #(< 50 (:gini %)) expectancy-and-gini)
      (sort-by :life-expectancy)))

;; ## å¹¸ç¦æ„Ÿ

;; è®©æˆ‘ä»¬æ¥çœ‹çœ‹å¹¸ç¦æ„Ÿï¼è¿™æ¬¡ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨ [jdbc.next](https://github.com/seancorfield/next-jdbc)
;; å¯¹ Sqlite æ•°æ®æ‰§è¡Œ SQL æŸ¥è¯¢ï¼Œè¯¥æ•°æ®åŒ…å«ä¸€ä¸ªå›½å®¶/åœ°åŒºåŠå…¶ç›¸å¯¹å¹¸ç¦åº¦è¯„åˆ†çš„è¡¨ã€‚
;; è¯·æ³¨æ„ï¼Œæˆ‘ä»¬æ­£åœ¨ä½¿ç”¨ `clojure.set` çš„ `rename-keys` å‡½æ•°å°†åˆ—å `:country_or_region` æ›´æ”¹ä¸º `:country`ï¼Œ
;; ä»¥ä¾¿æ­¤è¡¨æ˜“äºä¸æˆ‘ä»¬çš„å…¶ä»–è¡¨è¿æ¥ã€‚
(def world-happiness
  (let [_run-at #inst "2021-11-26T08:28:29.445-00:00" ; æ›´æ”¹æ­¤å€¼ä»¥é‡æ–°è¿è¡ŒæŸ¥è¯¢ï¼
        ds (jdbc/get-datasource {:dbtype "sqlite" :dbname "./datasets/happiness.db"})]
    (->> (with-open [conn (jdbc/get-connection ds)]
           (jdbc/execute! conn ["SELECT * FROM happiness"]
                          {:return-keys true :builder-fn rs/as-unqualified-lower-maps}))
         (map #(rename-keys % {:country_or_region :country})))))

;; æŸ¥çœ‹å¹¸ç¦æ„Ÿæ•°æ®ï¼Œä¼¼ä¹æ‰€æœ‰å¸¸è§çš„å«Œç–‘äººâ€”â€”åŒ—æ¬§äººã€è¥¿æ¬§äººã€åŠ æ‹¿å¤§äººå’Œæ–°è¥¿å…°äººâ€”â€”
;; éƒ½æ ¹æ®è‡ªå·±çš„ä¼°è®¡è¿‡ç€ç›¸å½“ç¾å¥½çš„ç”Ÿæ´»ã€‚
;; ä»”ç»†è§‚å¯Ÿï¼Œæˆ‘ä»¬å‘ç°è™½ç„¶å‰ 20 ä¸ªå›½å®¶/åœ°åŒºéƒ½ç›¸å¯¹å¯Œè£•ï¼Œä½†å¾ˆæ˜æ˜¾ GDP ä¸_è¯¥é˜Ÿåˆ—å†…çš„_å¹¸ç¦æ„Ÿæ²¡æœ‰å¾ˆå¼ºçš„ç›¸å…³æ€§ã€‚
(clerk/table world-happiness)

;; æ¥ä¸‹æ¥ï¼Œæˆ‘ä»¬ä½¿ç”¨ [kixi.stats](https://github.com/MastodonC/kixi.stats) ä¸ºæ­¤æ•°æ®é›†è®¡ç®—çº¿æ€§å›å½’ã€‚
^{::clerk/viewer {:transform-fn (clerk/update-val kixi-p/parameters)}}
(def linear-regression
  (transduce identity (kixi-stats/simple-linear-regression :score :gdp) world-happiness))

;; æˆ‘ä»¬å°†ä½¿ç”¨æ­¤çº¿æ€§å›å½’æ¥æ‰©å……æˆ‘ä»¬çš„æ•°æ®é›†ï¼Œä»¥ä¾¿æ¯ä¸ªæ•°æ®ç‚¹ä¹Ÿè·å¾—ä¸€ä¸ª `:regression` å€¼ã€‚
(def world-happiness+regression
  (mapv (fn [{:as datapoint :keys [score]}]
          (assoc datapoint :regression (kixi-p/measure linear-regression score)))
        world-happiness))

;; è®©æˆ‘ä»¬ç»˜åˆ¶å¹¸ç¦æ„Ÿå’Œ GDP ä¹‹é—´çš„å…³ç³»å›¾ï¼Œä»¥ä»æ•´ä½“ä¸Šäº†è§£æƒ…å†µã€‚
;; æ‚¨å¯ä»¥å°†é¼ æ ‡æ‚¬åœåœ¨å•ä¸ªæ•°æ®ç‚¹ä¸Šä»¥è·å–æ›´å¤šä¿¡æ¯ï¼š

^{:nextjournal.clerk/visibility {:code :hide :result :show}}
(clerk/vl
 {:data {:values world-happiness+regression}
  :width 700
  :height 500
  :layer [{:mark {:type :point}
           :encoding {:x {:field :score
                          :type :quantitative
                          :scale {:zero false}}
                      :y {:field :gdp
                          :type :quantitative}
                      :tooltip {:field :country}}}
          {:mark {:type :line :color "#ccc"}
           :encoding {:x {:field :score
                          :type :quantitative
                          :scale {:zero false}}
                      :y {:field :regression
                          :type :quantitative}}}]})

;; çœ‹èµ·æ¥ï¼Œæ­£å¦‚æˆ‘ä»¬å¯èƒ½é¢„æ–™çš„é‚£æ ·ï¼Œä¸€èˆ¬æ¥è¯´ï¼Œå¯Œè£•å›½å®¶æ¯”è´«ç©·å›½å®¶æ›´å¹¸ç¦ï¼Œå°½ç®¡å­˜åœ¨å·®å¼‚å’Œå¼‚å¸¸å€¼ã€‚
;; ä¾‹å¦‚ï¼ŒèŠ¬å…°æ’åç¬¬ä¸€ï¼Œä½†äººå‡ GDP ä¸ç¬¬ 58 åæ—¥æœ¬ç›¸ä¼¼ã€‚
;; ä¹Ÿè®¸æ›´å¼•äººæ³¨ç›®çš„æ˜¯ï¼Œå¡å¡”å°”çš„æ•°æ®é›†ä¸­äººå‡ GDP æœ€é«˜ï¼Œä½†å¡å¡”å°”äººçš„å¹³å‡å¹¸ç¦æ„Ÿä¸è¨å°”ç“¦å¤šçš„äººæ°‘å¤§è‡´ç›¸åŒã€‚
;; åŒæ ·ï¼ŒåšèŒ¨ç“¦çº³çš„äººå‡ GDP æ˜¯é©¬æ‹‰ç»´çš„äº”å€ï¼Œä½†å®ƒçš„äººæ°‘å¹¶æ²¡æœ‰å› æ­¤è€Œæ›´å¹¸ç¦ã€‚
;; å¦‚æœæˆ‘è¢«è¿«çŒœæµ‹åŸå› ï¼Œæˆ‘å¯èƒ½ä¼šæ¨æµ‹ï¼Œä¸€ä¸ªç¹è£çš„å›½å®¶ï¼Œå…¶æ‰€æœ‰è´¢å¯Œéƒ½é›†ä¸­åœ¨æå°‘æ•°äººæ‰‹ä¸­ï¼Œå¯¹äºæ™®é€šäººæ¥è¯´ä»ç„¶æ˜¯ä¸€ä¸ªç›¸å½“ç³Ÿç³•çš„å±…ä½åœ°ã€‚

;; è°ƒæŸ¥è¿™ç§å¯èƒ½æ€§çš„ä¸€ç§æ–¹æ³•æ˜¯ç»˜åˆ¶å¯Œè£•ä¸–ç•Œä¸­å¹³ç­‰ä¸å¹¸ç¦æ„Ÿä¹‹é—´çš„ç›¸å…³æ€§ã€‚
;; æˆ‘ä»¬å°†å†æ¬¡ä½¿ç”¨ `join`ï¼Œä½†æˆ‘ä»¬å°†é¦–å…ˆä½¿ç”¨ `clojure.set` çš„ `project`ï¼ˆç±»ä¼¼äº SQL æŠ•å½±ï¼‰
;; ä»å¹¸ç¦æ„Ÿæ•°æ®é›†ä¸­ä»…æå– `:country` å’Œ `:score`ï¼Œç„¶åæŒ‰ GDP æ’åºå¹¶å–å‰ 20 ä¸ªå›½å®¶/åœ°åŒºã€‚
(clerk/vl
 {:data {:values (->> (project world-happiness [:country :score])
                      (join expectancy-and-gini)
                      (sort-by :gdp >)
                      (take 20))}
  :width 700
  :height 500
  :mark {:type :point}
  :encoding {:x {:field :score
                 :type :quantitative
                 :scale {:zero false}}
             :y {:field :gini
                 :type :quantitative
                 :scale {:zero false}}
             :tooltip {:field :country}}})

;; è‡³å°‘ä¹ä¸€çœ‹ï¼Œè¿™ç¡®å®æ”¯æŒäº†è¿™æ ·ä¸€ç§è§‚ç‚¹ï¼Œå³æœ€å¹¸ç¦çš„äººâ€”â€”å°±åƒå¯¿å‘½æœ€é•¿çš„äººä¸€æ ·â€”â€”å€¾å‘äºå±…ä½åœ¨åŸºå°¼åˆ†å¸ƒä¸­æ›´å¹³ç­‰çš„éƒ¨åˆ†çš„å›½å®¶ã€‚

;; æˆ‘å¸Œæœ›è¿™ä¸ªä¾‹å­èƒ½ç»™ä½ ä¸€äº›ä½ æƒ³è¦è°ƒæŸ¥çš„æƒ³æ³•ã€‚
